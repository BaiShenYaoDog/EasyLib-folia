plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id("com.github.johnrengelman.shadow") version("7.1.2")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group = 'com.xbaimiao'
    version = '3.1.1'

    repositories {
        mavenCentral()
        maven { url 'https://maven.xbaimiao.com/repository/releases/' }
        maven { url 'https://repo.aikar.co/content/groups/aikar/' }
        maven { url "https://repo.dmulloy2.net/repository/public/" }
        maven { url "https://repo.tabooproject.org/repository/releases/" }
        maven {
            name 'CodeMC'
            url 'https://repo.codemc.org/repository/maven-public/'
        }
        maven {
            name 'PaperMC'
            url 'https://repo.papermc.io/repository/maven-snapshots/'
        }
    }

    dependencies {
        compileOnly("org.jetbrains.kotlin:kotlin-stdlib:1.7.10")
        compileOnly("com.j256.ormlite:ormlite-core:6.1")
        compileOnly("com.j256.ormlite:ormlite-jdbc:6.1")
        compileOnly("com.zaxxer:HikariCP:4.0.3")
        compileOnly("com.comphenix.protocol:ProtocolLib:4.7.0")
        compileOnly("public:vault:1.0.0")
        compileOnly("public:points:1.0.0")
        compileOnly("net.luckperms:api:5.4")
        compileOnly("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10")
        compileOnly("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
        compileOnly("redis.clients:jedis:5.0.1")
    }

    compileKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }

}

dependencies {
    def reflexVersion = "1.0.19"
    compileOnly "net.kyori:adventure-api:4.13.1"
    compileOnly("public:paper:1.16.5")
    compileOnly("public:papi:1.0.0")
    compileOnly(fileTree("libs"))

    // 本体
    implementation("org.tabooproject.reflex:analyser:${reflexVersion}")
    implementation("org.tabooproject.reflex:fast-instance-getter:${reflexVersion}")
    implementation("org.tabooproject.reflex:reflex:${reflexVersion}") // 需要 analyser 模块
    // 本体依赖
    implementation("org.ow2.asm:asm:9.2")
    implementation("org.ow2.asm:asm-util:9.2")
    implementation("org.ow2.asm:asm-commons:9.2")

    implementation(project(":easylib-api"))
    implementation(project(":easylib-folia"))
    implementation(project(":easylib-bukkit"))
    implementation(project(":easylib-bukkit-common"))
    implementation(project(":easylib-gson"))
    implementation 'public:ik:1.0.0'
    implementation 'com.github.cryptomorin:XSeries:9.4.0'
}

tasks.register('sourcesJar', Jar) {
    it.group("build")
    classifier = 'sources'
    subprojects {
        from sourceSets.main.allSource
    }
    from sourceSets.main.allSource
}

tasks {
    build {
        artifacts {
            archives(shadowJar)
//            archives(jar)
            archives(sourcesJar)
//            archives(kotlinSourcesJar)
        }
    }
    assemble {
        dependsOn(clean)
    }
    shadowJar {
        classifier = ''
        dependencies {
            exclude(dependency("org.jetbrains.kotlin:"))
            exclude(dependency("org.jetbrains:"))
            exclude(dependency("org.jetbrains.kotlinx:"))
        }
        relocate("org.wltea.expression", "com.xbaimiao.easylib.expression")
        relocate("com.cryptomorin.xseries", "com.xbaimiao.easylib.xseries")
        relocate("org.tabooproject.reflex", "com.xbaimiao.easylib.reflex")
        relocate("org.objectweb.asm", "com.xbaimiao.easylib.asm")
    }
    // UTF-8
    compileJava {
        options.encoding = "UTF-8"
    }
}

publishing {
    repositories {
        mavenLocal()
        maven {
            credentials {
                username = project.findProperty("BaiUser").toString()
                password = project.findProperty("BaiPassword").toString()
            }
            url("https://maven.xbaimiao.com/repository/releases/")
        }
    }
    publications {
        mavenJava(MavenPublication) {
//            artifact jar
            artifact sourcesJar
            artifact shadowJar
        }
    }
}